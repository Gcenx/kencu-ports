# -*- coding: utf-8; mode: tcl; tab-width: 4; indent-tabs-mode: nil; c-basic-offset: 4 -*- vim:fenc=utf-8:ft=tcl:et:sw=4:ts=4:sts=4

PortSystem                  1.0
PortGroup                   muniversal 1.0
PortGroup                   compiler_blacklist_versions 1.0
PortGroup                   active_variants 1.1
PortGroup                   legacysupport 1.1

name                        ffmpeg
conflicts                   ffmpeg-devel

# Please increase the revision of mpv whenever ffmpeg's version is updated.
version                     4.4.4
revision                    3
epoch                       1

license                     GPL-2+
categories                  multimedia
maintainers                 nomaintainer

description                 FFmpeg is a complete solution to play, record, convert and \
                            stream audio and video.
long_description            ${description}
homepage                    https://ffmpeg.org/

master_sites                ${homepage}releases/
use_xz                      yes

checksums                   rmd160  ac9916e0915cfe3952993644d0a26d9e7c70e1b7 \
                            sha256  e80b380d595c809060f66f96a5d849511ef4a76a26b76eacf5778b94c3570309 \
                            size    9565584

patchfiles-append           patch-libavcodec-audiotoolboxenc.c.diff

# Fix an upstream bug that overrides the max_b_frames setting
# https://trac.ffmpeg.org/ticket/9231
# Fixed via upstream commit: 55d9d6767967794edcdd6e1bbd8840fc6f4e9315
patchfiles-append           patch-libavcodec-videotoolboxenc.c.diff

# Patch for upstream bug related to non-B-frame encoding
# https://trac.ffmpeg.org/ticket/9439
# Fixed via upstream commit: b786bc7433dfe082441a57c1ba9ae9ea47904b78
patchfiles-append           patch-issue-9439-non-b-frame-encoding.diff

# Patch for upstream issue:
# libavcodec/libsvtav1.c:124:53: error: no member named 'compressed_ten_bit_format' in 'struct EbSvtAv1EncConfiguration'
#         (config->encoder_bit_depth > 8) && (config->compressed_ten_bit_format == 0) ? 1 : 0;
#                                             ~~~~~~  ^
# https://trac.macports.org/ticket/67327
# Fixed via upstream commit: 031f1561cd286596cdb374da32f8aa816ce3b135
patchfiles-append           patch-libavcodec-libsvtav1-ten_bit_format.diff

# Patch for building docs with texinfo >= 7
# https://trac.macports.org/ticket/68747
# https://trac.ffmpeg.org/ticket/10636
# Fixed via upstream commit: f01fdedb69e4accb1d1555106d8f682ff1f1ddc7
patchfiles-append           patch-texinfo-7.diff

# requires a C11 compiler
compiler.c_standard         2011

# warning: unknown warning option '-Werror=partial-availability'; did you mean '-Werror=availability'? [-Wunknown-warning-option]
# warning: unknown warning option '-Wno-bool-operation'; did you mean '-Wno-bool-conversion'? [-Wunknown-warning-option]
compiler.blacklist-append   {clang < 800}

# The old ffmpeg port was GPL-2+ as base and had a no_gpl variant, so this keeps us consistent
# Also, -gpl2 causes other ports to fail to build due to the missing libpostproc (#35473)
default_variants-append     +gpl2

if {[tbool configure.ccache]} {
    set ccache_path         ${prefix}/bin/ccache
    configure.cc-prepend    ${ccache_path}
    configure.cxx-prepend   ${ccache_path}
}

depends_build-append        port:cctools \
                            port:gmake \
                            port:pkgconfig \
                            port:texinfo

depends_lib-append          port:aom \
                            port:bzip2 \
                            port:dav1d \
                            port:fontconfig \
                            port:freetype \
                            port:fribidi \
                            path:lib/pkgconfig/gnutls.pc:gnutls \
                            path:lib/pkgconfig/harfbuzz.pc:harfbuzz \
                            port:lame \
                            port:libass \
                            port:libbluray \
                            port:libiconv \
                            port:libmodplug \
                            port:libogg \
                            port:libopus \
                            path:lib/pkgconfig/librsvg-2.0.pc:librsvg \
                            port:libsdl2 \
                            port:libtheora \
                            port:libvidstab \
                            port:libvorbis \
                            path:lib/pkgconfig/vpx.pc:libvpx \
                            port:openjpeg \
                            port:soxr \
                            path:lib/libspeex.dylib:speex \
                            port:svt-av1 \
                            port:x264 \
                            port:x265 \
                            port:XviD \
                            port:xz \
                            port:zimg \
                            port:zlib \
                            port:zvbi


configure.cflags-append     -DHAVE_LRINTF \
                            -Wno-deprecated-declarations \
                            ${configure.cppflags}

set port_sharedir           ${prefix}/share
set port_docdir             ${port_sharedir}/doc

configure.pre_args-append   --cc="${configure.cc}" \
                            --mandir=${prefix}/share/man

configure.args-append       --enable-audiotoolbox \
                            --enable-avfilter \
                            --enable-avresample \
                            --enable-fontconfig \
                            --enable-gpl \
                            --enable-gnutls \
                            --enable-libaom \
                            --enable-libass \
                            --enable-libbluray \
                            --enable-libdav1d \
                            --enable-libfreetype \
                            --enable-libfribidi \
                            --enable-libmodplug \
                            --enable-libmp3lame \
                            --enable-libopenjpeg \
                            --enable-libopus \
                            --enable-librsvg \
                            --enable-libsoxr \
                            --enable-libspeex \
                            --enable-libsvtav1 \
                            --enable-libtheora \
                            --enable-libvidstab \
                            --enable-libvorbis \
                            --enable-libvpx \
                            --enable-libx264 \
                            --enable-libx265 \
                            --enable-libxvid \
                            --enable-libzimg \
                            --enable-libzvbi \
                            --enable-lzma \
                            --enable-opencl \
                            --enable-pthreads \
                            --enable-postproc \
                            --enable-sdl2 \
                            --enable-shared \
                            --enable-swscale \
                            --enable-videotoolbox \
                            --enable-zlib

configure.args-append       --disable-outdev=xv \
                            --disable-indev=jack \
                            --disable-libjack \
                            --disable-libopencore-amrnb \
                            --disable-libopencore-amrwb \
                            --disable-libxcb \
                            --disable-libxcb-shm \
                            --disable-libxcb-xfixes \
                            --disable-securetransport

post-extract {
    # fix file perms; tarball contents deny group and world read
    system "find ${worksrcpath} -type d -print0 | xargs -0 chmod a+rx"
    system "find ${worksrcpath} -type f -print0 | xargs -0 chmod a+r"
}

platform darwin {

    if {${os.major} < 9} {

        # disable asm on Tiger -- CHECK THIS
        configure.args-append   --disable-asm

        post-patch {
            reinplace "s:,-compatibility_version,$\(LIBMAJOR\)::" ${worksrcpath}/configure
        }
    
        # libblueray requires 10.5+ -- CHECK THIS
        depends_lib-delete      port:libbluray
        configure.args-replace  --enable-libbluray \
                                --disable-libbluray
    
        depends_lib-delete      port:aom
        configure.args-replace  --enable-libaom \
                                --disable-libaom

        depends_lib-delete      port:svt-av1
        configure.args-replace  --enable-libsvtav1 \
                                --disable-libsvtav1
    }

    if {${os.major} < 10} {

        # libvpx requires darwin 10+ -- CHECK THIS
        depends_lib-delete      path:lib/pkgconfig/vpx.pc:libvpx
        configure.args-replace  --enable-libvpx \
                                --disable-libvpx

        # libsdl2 requires darwin 10+
        depends_lib-delete      port:libsdl2
        configure.args-replace  --enable-sdl2 \
                                --disable-sdl2

    }

    if {${os.major} < 11} {

        # filters coreimage and coreimagesrc don't build on 10.6
        # and earlier due to use of bridged casts in Objective C (#51823)
        configure.args-append   --disable-filter=coreimage \
                                --disable-filter=coreimagesrc

        # AudioToolbox support requires CoreMedia Framework available on 10.7+
        configure.args-replace  --enable-audiotoolbox \
                                --disable-audiotoolbox
    }

    if {${os.major} < 12} {

        # VideoToolbox is supported on 10.8+
        # It provides support for H265, H264, H263, MPEG1, MPEG2 and MPEG4.
        configure.args-replace  --enable-videotoolbox \
                                --disable-videotoolbox

        # OpenCL support requires version 1.2 available 10.8+
        configure.args-replace  --enable-opencl \
                                --disable-opencl

        # avfoundation is only available on 10.7+
        # as of ffmpeg 3.4.1 build fails on 10.7 as well
        # libavdevice/avfoundation.m:207:14: error: expected method to read dictionary element not found on object of type 'NSDictionary *'
        configure.args-append   --disable-indev=avfoundation
    }

}

    # Apple GCC has problems with SIMD intrinsics and -Werror=no-missing-prototypes.
#    if {${os.major} < 11} {
#        patchfiles-append \
#                    patch-configure-no-error-on-missing-prototypes.diff
#    }


platform darwin powerpc {
    # absence of altivec is not automatically detected
    if {[catch {sysctl hw.vectorunit} result] || $result == 0} {
        configure.args-append   --disable-altivec

        # as this machine is a G3, force a local build so we don't download a buildbot-built
        # version possibly built on a G4+ or cross-compiled from an Intel system
        archive_sites
    }

    configure.ldflags-append   -latomic
}

configure.universal_args-delete --disable-dependency-tracking

if {${universal_possible} && [variant_isset universal]} {
    foreach arch ${configure.universal_archs} {
        set merger_host($arch) ""
        lappend merger_configure_args($arch)  --arch=${arch}
        lappend merger_configure_env($arch)   "ASFLAGS=-arch ${arch}"
    }
    if {[string match "*86*" ${configure.universal_archs}]} {
        depends_build-append    port:nasm
    }
    lappend merger_configure_args(i386)    --enable-x86asm
    lappend merger_configure_args(x86_64)  --enable-x86asm
} else {
    configure.args-append       --arch=${configure.build_arch}
    configure.env-append        ASFLAGS=[get_canonical_archflags]
    if {${build_arch} eq "i386" || ${build_arch} eq "x86_64"} {
        depends_build-append    port:nasm
        configure.args-append   --enable-x86asm
    }
}

build.cmd           ${prefix}/bin/gmake
build.env-append    V=1

test.run            yes

destroot.env-append V=1

variant x11 {
    # enable x11grab_xcb input device
    depends_lib-append      port:xorg-libxcb \
                            port:xorg-libXext \
                            port:xorg-libXfixes
    configure.args-delete   --disable-xlib \
                            --disable-libxcb \
                            --disable-libxcb-shm \
                            --disable-libxcb-xfixes
}

if {[variant_isset x11]} {
    require_active_variants libsdl2 x11
} else {
    require_active_variants libsdl2 "" x11
}

variant libdc1394 description {Enable IIDC-1394 frame grabbing using libdc1394 (experimental)} {
    depends_lib-append      port:libdc1394
    configure.args-append   --enable-libdc1394
}

variant jack description {Enable jack library and indev support} {
    # jack will autoconfigure if not disabled
    depends_lib-append      port:jack
    configure.args-replace  --disable-libjack \
                            --enable-libjack
    configure.args-replace  --disable-indev=jack \
                            --enable-indev=jack
}

variant gpl3 description {Enable GPL code, license will be GPL-3+} {
    license         GPL-3+

    configure.args-append   --enable-libaribb24 \
                            --enable-libsmbclient \
                            --enable-version3
    depends_lib-append      port:libaribb24 \
                            path:lib/pkgconfig/smbclient.pc:samba3
}

variant nonfree description {enable nonfree code, libraries and binaries will not be redistributable} {
    license         Restrictive

    configure.args-append   --enable-libfdk-aac \
                            --enable-nonfree
    depends_lib-append      port:libfdk-aac
}

variant rav1e description {Enable codec rav1e} {
    # rav1e available on 10.6+
    if {(${os.major} < 10)} {
        error "Variant rav1e not supported for macOS 10.5 and earlier"
    } else {
        configure.args-append   --enable-librav1e
        depends_lib-append      port:rav1e
    }
}

livecheck.type      regex
livecheck.url       ${master_sites}
livecheck.regex     "${name}-(4(?:\\.\\d+)*)${extract.suffix}"
